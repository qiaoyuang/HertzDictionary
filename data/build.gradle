plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'com.android.library'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

repositories {
    mavenCentral()
    jcenter()
    google()
}
group 'com.w10group.hertzdictionary'
version '0.0.1'

apply plugin: 'maven-publish'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation":"$projectDir/schemas".toString(),
                        "room.incremental":"true",
                        "room.expandProjection":"true"]
            }
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
            java.srcDirs += 'src/androidMain/kotlin'
            res.srcDirs += 'src/androidMain/res'
            assets.srcDirs += 'src/androidMain/assets'
        }
        androidTest {
            java.srcDirs += 'src/androidTest/kotlin'
            res.srcDirs += 'src/androidTest/res'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

kotlin {
    // This is for iPhone simulator
    // Switch here to iosArm64 (or iosArm32) to build library for iPhone device
    iosX64("ios") {
        binaries {
            framework {
                freeCompilerArgs += '-Xobjc-generics'
                isStatic = true
            }
        }
    }
    android("android") {
        // publishLibraryVariants("release", "debug")
    }
    sourceSets {
        all {
            languageSettings.useExperimentalAnnotation("kotlin.Experimental")
        }
        commonMain {
            dependencies {
                // Kotlin 扩展类库
                api kotlin('stdlib-common')
                api "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:$serialization_version"

                // Ktor
                implementation "io.ktor:ktor-client-core:$ktor_version"
                implementation "io.ktor:ktor-client-serialization:$ktor_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        androidMain {
            dependencies {
                implementation fileTree(include: ['*.jar'], dir: 'libs')

                // Kotlin 扩展类库
                //api kotlin('stdlib')
                api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
                api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
                // implementation "org.jetbrains.kotlinx:kotlinx-io-jvm:0.1.16"
                // implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

                // Ktor
                implementation "io.ktor:ktor-client-okhttp:$ktor_version"
                // implementation "io.ktor:ktor-client-serialization-jvm:$ktor_version"

                // Room
                implementation "androidx.room:room-runtime:$room_version"
                implementation "androidx.room:room-ktx:$room_version"
                compileOnly 'javax.annotation:javax.annotation-api:1.3.2'

                // Square
                implementation 'com.squareup.okio:okio:2.8.0'
                implementation 'com.squareup.okhttp3:okhttp:4.8.1'
            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        iosMain {
            dependencies {
                // Ktor
                implementation "io.ktor:ktor-client-ios:$ktor_version"
                // implementation "io.ktor:ktor-client-serialization-native:$ktor_version"
            }
        }
        iosTest {
        }
    }
}

task myIosTest {
    def device = project.findProperty("iosDevice")?.toString() ?: "iPhone 8"
    dependsOn kotlin.targets.ios.binaries.getTest('DEBUG').linkTaskName
    group = JavaBasePlugin.VERIFICATION_GROUP
    description = "Runs tests for target 'ios' on an iOS simulator"

    doLast {
        def binary = kotlin.targets.ios.binaries.getTest('DEBUG').outputFile
        exec {
            commandLine 'xcrun', 'simctl', 'spawn', device, binary.absolutePath
        }
    }
}

dependencies {
    kapt "androidx.room:room-compiler:$room_version"
}

configurations {
    compileClasspath
}